title: Relational Reponding Task 
authors: Yang Ye (yang.ye@ugent.be), Ian Hussey (ian.hussey@ugent.be)
license: GPLv3+
reference: De Houwer, J., Heider, N., Spruyt, A., Roets, A., & Hughes, S. (2015). 
 The relational responding task: Toward a new implicit measure of beliefs. Frontiers in Psychology, 6.

*****************************************************************
relation labels
*****************************************************************

<item relation_1_label>
/ 1 = "TRUE"
</item>

<item relation_2_label>
/ 1 = "FALSE"
</item>

*****************************************************************
relation synonyms
*****************************************************************

<item relation_1_item>
/ 1 = "correct"
/ 2 = "confirm"
/ 3 = "right"
/ 4 = "accurate"
/ 5 = "yes"
/ 6 = "valid"
/ 7 = "definite"
/ 8 = "sure"
</item>

<item relation_2_item>
/ 1 = "incorrect"
/ 2 = "deny"
/ 3 = "wrong"
/ 4 = "inaccurate"
/ 5 = "no"
/ 6 = "invalid"
/ 7 = "inexact"
/ 8 = "untrue"
</item>

*****************************************************************
statements
*****************************************************************

<item proposition_A_item>
/ 1 = "cigarettes cause cancer"
/ 2 = "cars cause pollution"
/ 3 = "death causes grief"
/ 4 = "exams cause stress"
/ 5 = "vaccines prevent disease"
/ 6 = "cholesterol causes heart−disease"
/ 7 = "coffee prevents sleep"
/ 8 = "deodorant prevents smell"
/ 9 = "exercise prevents heart−disease"
/ 10 = "safety prevents injury"
</item>

<item proposition_B_item>
/ 1 = "cigarettes prevent cancer"
/ 2 = "cars prevent pollution"
/ 3 = "death prevents grief"
/ 4 = "exams prevent stress"
/ 5 = "cholesterol prevents heart−disease"
/ 6 = "vaccines cause disease"
/ 7 = "coffee causes sleep"
/ 8 = "deodorant causes smell"
/ 9 = "exercise causes heart−disease"
/ 10 = "safety causes injury"
</item>

*****************************************************************
instructions
*****************************************************************

NB construction of instructions is difficult to autmomate given that one or both blocks may or may not have 
rules that make sense in an everyday sense. The below should therefore be customised as necessary, with 
an emphasis on "as if" responding.

<item instructions_item>
/ 1 = 
"In this task you will have to react to the words and statments presented on screen. 

In the first phase words will be presented in orange.

Categorize them as meaning FALSE or TRUE using the E and I keys.

	Press E if the word means FALSE.
	Press I if the word means TRUE."
/ 2 = 
"Please respond as quickly as possible without making too many mistakes. 

If you make a mistake, press the correct key as soon as possible to continue."
/ 3 = 
"In the next phase statements will be presented in white.

Respond to the statements as being generally considered to be FALSE or TRUE using the E and I keys. 
If you're unsure, use the feedback to learn the required response.
	
	Press I when the statement is FALSE.
	Press E when the statement is TRUE."
/ 4 = 
"For example:

	If presented with 'cigarettes cause cancer' respond with I for TRUE.

	If presented with 'vaccines prevent disease' respond with E for FALSE."
/ 5 = 
"The next phase will present both the orange words and the white statements together. Please continue to respond as you have before."
/ 6 = 
"The next phase will present the statements in white only.

IMPORTANT: This time, you must respond to true statements AS IF they are false, and false statements AS IF they are true.

	When the statement is true, respond with E for FALSE.
	When the statement is false, respond with I for TRUE."
/ 7 = 
"For example:

	If presented with 'cigarettes cause cancer' respond with I for TRUE.

	If presented with 'vaccines prevent disease' respond with E for FALSE."
/ 8 = 
"The next phase will present both the orange words and the white statements together.

Again, you must respond to true statements AS IF they are false, and false statements AS IF they are true.

You must also categorize the words in ORANGE as meaning TRUE or FALSE, as you have before."
</item>

*****************************************************************
stimuli
*****************************************************************

<text relation_1>
/ items = relation_1_item
/ fontstyle = ("Arial", 4%)
/ txcolor = (255, 128, 0)
/ select = noreplacenorepeat
</text>

<text relation_2>
/ items = relation_2_item
/ fontstyle = ("Arial", 4%)
/ txcolor = (255, 128, 0)
/ select = noreplacenorepeat
</text>

<text relation_1_label>
/ items = relation_1_label
/ halign = right
/ position = (95%, 10%)
/ fontstyle = ("Arial", 5%, true)
</text>

<text relation_2_label>
/ items = relation_2_label
/ halign = left
/ position = (5%, 10%)
/ fontstyle = ("Arial", 5%, true)
</text>

<text proposition_A>
/ items = proposition_A_item
/ fontstyle = ("Arial", 4%)
/ select = noreplacenorepeat
</text>

<text proposition_B>
/ items = proposition_B_item
/ fontstyle = ("Arial", 4%)
/ select = noreplacenorepeat
</text>

<text error>
/ position = (50%, 75%)
/ items = ("X")
/ color = (255, 0, 0)
/ fontstyle = ("Arial", 12%)
</text>

<counter instructions_counter>
/ resetinterval = 45
/ select = sequence(1, 2, 3, 4, 5, 6, 7)
</counter>

<text instructions_text>
/ items = instructions_item
/ hjustify = left
/ size = (85%, 60%)
/ select = instructions_counter
</text>

<text continue_text>
/ items = ("Press spacebar to continue.")
/ size = (50%, 8%)
/ position = (50%, 80%)
</text>

<text ready_text>
/ items = ("Place your index fingers on E and I. Press one of them to begin.")
</text>

*****************************************************************
trials
*****************************************************************

<trial relation_1_trial>
/ validresponse = ("e", "i")
/ correctresponse = ("i")
/ stimulusframes = [1 = relation_1]
/ posttrialpause = 300
</trial>

<trial relation_2_trial>
/ validresponse = ("e", "i")
/ correctresponse = ("e")
/ stimulusframes = [1 = relation_2]
/ posttrialpause = 300
</trial>

<trial proposition_A_relation_1_trial>
/ validresponse = ("e", "i")
/ correctresponse = ("i")
/ stimulusframes = [1 = proposition_A]
/ posttrialpause = 300
</trial>

<trial proposition_A_relation_2_trial>
/ validresponse = ("e", "i")
/ correctresponse = ("e")
/ stimulusframes = [1 = proposition_A]
/ posttrialpause = 300
</trial>

<trial proposition_B_relation_1_trial>
/ validresponse = ("e", "i")
/ correctresponse = ("i")
/ stimulusframes = [1 = proposition_B]
/ posttrialpause = 300
</trial>

<trial proposition_B_relation_2_trial>
/ validresponse = ("e", "i")
/ correctresponse = ("e")
/ stimulusframes = [1 = proposition_B]
/ posttrialpause = 300
</trial>

<trial instructions_trial>
/ stimulustimes = [1 = instructions_text, continue_text]
/ correctresponse = (57)
/ errormessage = false
/ recorddata = false
</trial>

<trial ready_trial>
/ stimulustimes = [1 = ready_text]
/ correctresponse = ("e", "i")
/ posttrialpause = 1000
/ errormessage = false
/ recorddata = false
</trial>

*****************************************************************
blocks
*****************************************************************

<block inducer_block>
/ bgstim = (relation_2_label, relation_1_label)
/ trials = [1-2 = instructions_trial; 
			3 = ready_trial; 
			4-43 = random(relation_1_trial, relation_2_trial)]
/ errormessage = true(error, 0)
/ responsemode = correct
</block>

<block proposition_A_relation_1_block>
/ bgstim = (relation_2_label, relation_1_label)
/ trials = [1-2 = instructions_trial; 
			3 = ready_trial; 
			4-43 = random(proposition_A_relation_1_trial, proposition_B_relation_2_trial)]
/ errormessage = true(error, 0)
/ responsemode = correct
</block>

<block proposition_A_relation_1_and_inducers_block_1>
/ bgstim = (relation_2_label, relation_1_label)
/ trials = [1 = instructions_trial; 
			2 = ready_trial; 
			3-42 = random(relation_1_trial, relation_2_trial, proposition_A_relation_1_trial, proposition_B_relation_2_trial)]
/ errormessage = true(error, 0)
/ responsemode = correct
</block>

<block proposition_A_relation_1_and_inducers_block_2>
/ bgstim = (relation_2_label, relation_1_label)
/ trials = [1-40 = random(relation_1_trial, relation_2_trial, proposition_A_relation_1_trial, proposition_B_relation_2_trial)]
/ errormessage = true(error, 0)
/ responsemode = correct
</block>

<block proposition_B_relation_1_block>
/ bgstim = (relation_2_label, relation_1_label)
/ trials = [1-2 = instructions_trial; 
			3 = ready_trial; 4-43 = random(proposition_B_relation_1_trial, proposition_A_relation_2_trial)]
/ errormessage = true(error, 0)
/ responsemode = correct
</block>

<block proposition_B_relation_1_and_inducers_block_1>
/ bgstim = (relation_2_label, relation_1_label)
/ trials = [1 = instructions_trial; 
			2 = ready_trial; 
			3-42 = random(relation_1_trial, relation_2_trial, proposition_B_relation_1_trial, proposition_A_relation_2_trial)]
/ errormessage = true(error, 0)
/ responsemode = correct
</block>

<block proposition_B_relation_1_and_inducers_block_2>
/ bgstim = (relation_2_label, relation_1_label)
/ trials = [1-40 = random(relation_1_trial, relation_2_trial, proposition_B_relation_1_trial, proposition_A_relation_2_trial)]
/ errormessage = true(error, 0)
/ responsemode = correct
</block>

*****************************************************************
experiment
*****************************************************************

<defaults>
/ screencolor = (0, 0, 0)
/ txbgcolor = (0, 0, 0)
/ txcolor = (255, 255, 255)
/ fontstyle = ("Arial", 3%) 
</defaults>

<expt>
/ blocks = [1 = block1; 2 = block2; 3 = block3; 4 = block4; 5 = block5; 6 = block6; 7 = block7]
</expt>

<variables>
/ group = (1 of 1)(block1 = inducer_block, 
					block2 = proposition_A_relation_1_block, 
					block3 = proposition_A_relation_1_and_inducers_block_1, 
					block4 = proposition_A_relation_1_and_inducers_block_2, 
					block5 = proposition_B_relation_1_block, 
					block6 = proposition_B_relation_1_and_inducers_block_1, 
					block7 = proposition_B_relation_1_and_inducers_block_2) 
/ groupassignment = subjectnumber
</variables>

*****************************************************************
data
*****************************************************************

<data>
/ columns = [date, time, subject, blockcode, blocknum, trialcode, trialnum, response, correct, latency, stimulusnumber, stimulusitem]
</data>

